#include <Servo.h>

#define num 6  

Servo servoMotors[num];

int servoPins[num] = {13, 12, 8, 7, 4, 2};
int Positions[num] = {90, 90, 90, 90, 90, 90};  
int current[num] = {0, 0, 0, 0, 0, 0};  

void setup() {
    Serial.begin(9600);
    
    for (int i = 0; i < num; i++) {
        servoMotors[i].attach(servoPins[i]);
        servoMotors[i].write(current[i]); 
    }

    Serial.println("Enter 6 angles separated by commas :");
}

void loop() {
    if (Serial.available()) {
        String inputString = Serial.readStringUntil('\n'); 
        parseAngles(inputString);
    }
    
    moveSmoothly();
}

void parseAngles(String input) {
    int idx = 0;
    char *token = strtok((char *)input.c_str(), ",");
    
    while (token != NULL && idx < num) {
        Positions[idx] = constrain(atoi(token), 0, 180);
        token = strtok(NULL, ",");
        idx++;
    }
}

void moveSmoothly() {
    bool stillMoving = false; 
    
    for (int i = 0; i < num; i++) {
        if (current[i] < Positions[i]) {
            current[i]++;
            stillMoving = true;
        } 
        else if (current[i] > Positions[i]) {
            current[i]--;
            stillMoving = true;
        }
        
        servoMotors[i].write(current[i]); 
    }

    if (stillMoving) {
        delay(15);
    }
}
